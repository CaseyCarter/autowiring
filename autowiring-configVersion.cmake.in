set(PACKAGE_VERSION "@autowiring_VERSION@")

if(autowiring_DEBUG)
  message(STATUS "Debug mode on")
  message(STATUS "Installed autowiring_VERSION: @autowiring_VERSION@")
  message(STATUS "Configured autowiring_ARCHITECTURE: ${autowiring_ARCHITECTURE}")
  message(STATUS "Installed CMAKE_SIZEOF_VOID_P: @CMAKE_SIZEOF_VOID_P@")
  message(STATUS "Installed using autowiring_USE_LIBCXX: @autowiring_USE_LIBCXX@")
  message(STATUS "Configured using autowiring_USE_LIBCXX: ${autowiring_USE_LIBCXX}")
endif()

# If the customer has an override architecture requirement, use that
if(DEFINED autowiring_ARCHITECTURE)
  string(REGEX MATCH "amd64|x86_64|x64" autowiring_is_x64 ${autowiring_ARCHITECTURE})
  string(REGEX MATCH "i386|x86" autowiring_is_x86 ${autowiring_ARCHITECTURE})
  string(REGEX MATCH "arm" autowiring_is_arm ${autowiring_ARCHITECTURE})

  # Clear the autowiring_ARCHITECTURE variable so we can tell if something went wrong
  set(autowiring_ARCHITECTURE)

  # Classify
  if(autowiring_is_x86)
    set(autowiring_ARCHITECTURE x86)
  elseif(autowiring_is_x64)
    set(autowiring_ARCHITECTURE x64)
  elseif(autowiring_is_arm)
    set(autowiring_ARCHITECTURE arm)
  endif()

  if(NOT DEFINED autowiring_ARCHITECTURE)
    message(WARNING "Unrecognized architecture ${autowiring_ARCHITECTURE}")
    set(autowiring_WORD_WIDTH -1)
  endif()
else()
  # Try to infer what the user wants
  if(CMAKE_SYSTEM_PROCESSOR STREQUAL "arm")
    set(autowiring_ARCHITECTURE arm)
  elseif(${CMAKE_SIZEOF_VOID_P} EQUAL 8)
    set(autowiring_ARCHITECTURE x64)
  else()
    set(autowiring_ARCHITECTURE x86)
  endif()
endif()

if(autowiring_DEBUG)
  message(STATUS "autowiring_ARCHITECTURE: ${autowiring_ARCHITECTURE}")
endif()

# Verify that we have a word width matching the bit depth desired by the customer
if(NOT autowiring_ARCHITECTURE STREQUAL "@autowiring_ARCHITECTURE@")
  set(PACKAGE_VERSION_COMPATIBLE FALSE)
  set(PACKAGE_VERSION_UNSUITABLE TRUE)
  if(autowiring_DEBUG)
    message(STATUS "Requested architecture of ${autowiring_ARCHITECTURE} not compatible with @autowiring_ARCHITECTURE@")
  endif()
  return()
endif()

# Determine whether the user's request (either implied or explicit) for libstdc++ can
# be met by this verison of Autowiring
if("${CMAKE_CXX_COMPILER_ID}" STREQUAL "AppleClang")
  # If this value isn't defined, then we assume the user's request is "on"
  if(NOT DEFINED autowiring_USE_LIBCXX)
    SET(autowiring_USE_LIBCXX ON)
  endif()

  # Our built version must be the same as the requested version.  If it's not, then we are
  # not a match for the user's request
  if((NOT ${autowiring_USE_LIBCXX} AND @autowiring_USE_LIBCXX@) OR (${autowiring_USE_LIBCXX} AND NOT @autowiring_USE_LIBCXX@))
    set(PACKAGE_VERSION_COMPATIBLE FALSE)
    set(PACKAGE_VERSION_UNSUITABLE TRUE)
  endif()
endif()

# Check whether the requested PACKAGE_FIND_VERSION is compatible
if("${PACKAGE_VERSION}" VERSION_LESS "${PACKAGE_FIND_VERSION}")
  set(PACKAGE_VERSION_COMPATIBLE FALSE)
else()
  set(PACKAGE_VERSION_COMPATIBLE TRUE)
  if ("${PACKAGE_VERSION}" VERSION_EQUAL "${PACKAGE_FIND_VERSION}")
    set(PACKAGE_VERSION_EXACT TRUE)
  endif()
endif()
